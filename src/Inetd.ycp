/**
 * File:	modules/Inetd.ycp
 * Package:	Configuration of inetd
 * Summary:	Data for configuration of inetd, input and output functions.
 * Authors:	Petr Hadraba <phadraba@suse.cz>
 *
 * $Id$
 *
 * Representation of the configuration of inetd.
 * Input and output routines.
 */

{

    /**
     * New SCR interface:
     * A service map looks like this:
     * $[
     *   "service": string	// * different from equally named field above
     *   "rpc_version": string
     *   "socket_type": string
     *   "protocol": string
     *   "wait": boolean
     *   "max": integer		// inetd only
     *   "user": string		// *
     *   "group": string
     *   "server": string
     *   "server_args": string
     *   "comment": string	// possibly multiline, without #
     ** bookkeeping fields:
     *   "iid": string		// internal id, use as table `id
     *      Iid is necessary because there may be multiple variants
     *      of the same service.
     *   "changed": boolean	// when writing, unchanged services are ignored
     *                          //   new services (created) must be set as changed
     *                          //   see changeLine() and addLine() for more details
     *   "deleted": boolean	// when deleting, this is set to TRUE and changed
     *                          // must be set too (see deleteLine())
     * ]
     * When handling existing maps, take care to preserve any other fields
     * that may be present!
     *
     * path netd = .whatever.inetd or .whatever.xinetd;
     *
     * SCR::Read (netd + .services) -> list of service maps
     *   There should be a warning if inetd and xinetd are both installed
//     * SCR::Read (netd + .services + service_name) -> service map (or empty)
//     * SCR::Read (netd + .services + service_name + field) -> (any) field
     * SCR::Write (netd, nil) -> boolean
     *   Commits changes performed by other writes (see below) to disk
     * // Hm, maybe we should check that vaid fields are passed?
     * SCR::Write (netd + .services, list of service maps) -> true
     * SCR::Write (netd + .services + service_name, service map) -> true
     * SCR::Write (netd + .services + service_name + field, any) -> true
     *   This last one should be useful for autoyast - changing just
     *   the "enabled" field.
     */

module "Inetd";
textdomain "inetd";

import "Progress";
import "Report";
import "Summary";

include "inetd/routines.ycp";
include "inetd/default_conf_inetd.ycp";
include "inetd/default_conf_xinetd.ycp";

/**
 * Abort function
 * return boolean return true if abort
 */
global block AbortFunction = nil;

/**
 * Data was modified?
 */
global boolean modified = false;

global boolean proposal_valid = false;
global boolean write_only = false;

/**
 * Data was modified?
 * @return true if modified
 */
global define boolean Modified() ``{
    //y2debug("modified=%1",modified);
    return modified;
};

/**
 * hold informations about installed services (rpm -q exit code).
 * 0 means installed, anything else means no or error
 */
global integer inetd_installed   = -1;
global integer xinetd_installed  = -1;

/**
 * What service is ready for configuration?
 * `none   means no available --- this is for AutoInstallation
 * `both   means the selection dialog will be displayed (only one service can run)
 * `inetd  inetd will be configured
 * `xinetd xinetd will be configured
 */
global symbol netd_service       = `none;

/**
 * What service was configured?
 * Same values as netd_service except `both
 */
global symbol configured_service = `none;

/**
 * These variables hold service's configuration
 */
global list inetd_conf           = [];
global list xinetd_conf          = [];

/**
 * Holds configuration of selected service.
 */
global list netd_conf            = [];

/**
 * Is inetd (or xinetd) running?
 */
global integer inetd_status      = -1;
global integer xinetd_status     = -1;

/**
 * Status of edited service (see WhatToConfigureDialog() in dialogs.ycp)
 */
global integer netd_status       = -1;

/**
 * Read all inetd settings
 * @return true on success
 */
global define boolean Read() ``{

    /* Inetd read dialog caption */
    string caption = _("Initializing inetd configuration");

    integer steps = 1;

    integer sl = 0;
    sleep(sl);

    Progress::New( caption, " ", steps, [
	    _("Read the configuration"),
	], [
	    _("Reading the configuration..."),
	    _("Finished")
	],
	""
    );

    integer read_status = 0;
    // read database
    if(Abort()) return false;

    if(netd_service == `none)
	return false;

    if(netd_service == `both) {
	inetd_conf  = SCR::Read(.etc.inetd_conf.services);
	xinetd_conf = SCR::Read(.etc.xinetd_conf.services);
    }
    else if(netd_service == `inetd) {
	inetd_conf = SCR::Read(.etc.inetd_conf.services);
    }
    else if(netd_service == `xinetd) {
	xinetd_conf = SCR::Read(.etc.xinetd_conf.services);
    }
    // in future: catch error
    if(false) Report::Error(_("Can not read the database1!"));
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Finished"));
    sleep(sl);

    if(Abort()) return false;
    modified = false;
    Progress::Finish();
    return true;
}

/**
 * Write all inetd settings
 * @return true on success
 */
global define boolean Write() ``{

    /* Inetd read dialog caption */
    string caption = _("Saving inetd configuration");

    integer steps = 1;

    integer sl = 0;
    sleep(sl);

    Progress::New(caption, " ", steps, [
	    _("Write the settings"),
	], [
	    _("Writing the settings..."),
	    _("Finished")
	],
	""
    );

    y2milestone("Calling write:\n");
    // write settings
    if(Abort()) return false;
//    y2milestone("%1, %2", configured_service, netd_status);
    if (write_only) {
	if (configured_service == `inetd) {
	    if (netd_status == 0) {
		Runlevel::ServiceAdjust("inetd", "enable");
	    }
	    else {
		Runlevel::ServiceAdjust("inetd", "disable");
	    }
	    Runlevel::ServiceAdjust("xinetd", "disable");
	    SCR::Write(.etc.inetd_conf.services, netd_conf);
	}
	else if (configured_service == `xinetd) {
	    if (netd_status == 0) {
		Runlevel::ServiceAdjust("xinetd", "enable");
	    }
	    else {
		Runlevel::ServiceAdjust("xinetd", "disable");
	    }
	    Runlevel::ServiceAdjust("inetd", "disable");
	    SCR::Write(.etc.xinetd_conf.services, netd_conf);
	}
    }
    else {
    if (netd_status != 0) {
	y2milestone("Nothing changed  --- installed services will be stopped");
	if (inetd_installed == 0) {
	    y2milestone("inetd installed  --- stopping and diabling service");
	    Runlevel::RunInitScript("inetd", "stop");
	    Runlevel::ServiceAdjust("inetd", "disable");
	}
	else {
	    y2milestone("inetd not installed --- nothing done");
	}
	if (xinetd_installed == 0) {
	    y2milestone("xinetd installed --- stopping and disabling service");
	    Runlevel::RunInitScript("xinetd", "stop");
	    Runlevel::ServiceAdjust("xinetd", "disable");
	}
	else {
	    y2milestone("xinetd not installed --- nothing done");
	}
    }
    // custom
    else {
	// configured service is inetd
	if (configured_service == `inetd) {
	    // xinetd must be stopped - if installed
	    if (xinetd_installed == 0) {
		y2milestone("xinetd installed  --- stopping and disabling service");
		Runlevel::RunInitScript("xinetd", "stop");
		Runlevel::ServiceAdjust("xinetd", "disable");
	    }
	    else {
		y2milestone("xinetd not installed --- nothing done");
	    }
	    y2milestone("writing inetd configuration");
	    SCR::Write(.etc.inetd_conf.services, netd_conf);
	    // So, Restart service
	    // if inetd is running - only reload
	    if (inetd_status == 0) {
		y2milestone("inetd was running --- calling force-reload");
		Runlevel::RunInitScript("inetd", "force-reload");
	    }
	    // if stopped - enable and start
	    else {
		y2milestone("inetd was stopped --- enabling and starting service");
		Runlevel::ServiceAdjust("inetd", "enable");
		Runlevel::RunInitScript("inetd", "start");
	    }
	}
	// configured service is xinetd
	else {
	    // inetd must be stopped - if installed
	    if (inetd_installed == 0) {
		y2milestone("inetd installed     --- stopping and disabling service");
		Runlevel::RunInitScript("inetd", "stop");
		Runlevel::ServiceAdjust("inetd", "disable");
	    }
	    else {
		y2milestone("inetd not installed --- nothing done");
	    }
	    y2milestone("writing xinetd cnfiguration");
	    SCR::Write(.etc.xinetd_conf.services, netd_conf);
	    // So, Restart service
	    // if xinetd is running - only reload
	    if (inetd_status == 0) {
		y2milestone("xinetd was running --- calling force-reload");
		Runlevel::RunInitScript("xinetd", "force-reload");
	    }
	    // if stopped - enable and start
	    else {
		y2milestone("xinetd was stopped --- enabling and starting service");
		Runlevel::ServiceAdjust("xinetd", "enable");
		Runlevel::RunInitScript("xinetd", "start");
	    }
	}
    }
    }

    y2milestone("Writting done\n");

    // in future: catch errors
    if(false) Report::Error (_("Can not write settings!"));
    sleep(sl);

    if(Abort()) return false;
    ProgressNextStage(_("Finished"));
    sleep(sl);

    if(Abort()) return false;
    Progress::Finish();
    return true;
}

/**
 * Only Write settings
 * @return boolean True on success
 */
global define boolean WriteOnly () ``{
    write_only = true;
    return Write();
}

/**
 * Get all inetd settings from the first parameter
 * (For use by autoinstallation.)
 * @param settings The YCP structure to be imported.
 * @return boolean True on success
 */
global define boolean Import (map settings) ``{
//    sleep(3000);
    /*
     * if map contains "netd_service", whole configuration is filled from this map.
     * if "netd_service" is not found, configuration is generated from default values
     * stored if default_conf_*.ycp.
     */
    if (settings["netd_service"]:`none == `inetd) {
	inetd_conf    = settings["netd_conf"]:[];
        inetd_status  = 0;
	netd_service  = `inetd;
	netd_status   = settings["netd_status"]:-1;
	netd_conf     = inetd_conf;
    }
    else if (settings["netd_service"]:`none == `xinetd) {
	xinetd_conf   = settings["netd_conf"]:[];
        xinetd_status = 0;
	netd_service  = `xinetd;
	netd_status   = settings["netd_status"]:-1;
	netd_conf     = xinetd_conf;
    }
    else if (settings["netd_service"]:`none == `none) {
	inetd_conf    = default_conf_inetd["netd_conf"]:[];
	xinetd_conf   = default_conf_xinetd["netd_conf"]:[];
	inetd_status  = 0;
	xinetd_status = 0;
	netd_service  = `both;
    }
    configured_service = netd_service;
    return true;
}

/**
 * Dump the inetd settings to a single map
 * (For use by autoinstallation.)
 * @return map Dumped settings (later acceptable by Import ())
 */
global define map Export () ``{
//    sleep(3000);
    map config = $[];
    config = add(config, "netd_conf",    netd_conf);
    config = add(config, "netd_service", configured_service);
    config = add(config, "netd_status",  netd_status);
    y2milestone("%1", config);
    return config;
//    return $[];
}

/**
 * Create a textual summary and a list of unconfigured cards
 * // @param split split configured and unconfigured?
 * @return summary of the current configuration
 */
global define list Summary() ``{
    // TODO FIXME: your code here...
    return [ _("Configuration summary ..."), [] ];
}

/**
 * Create an overview table with all configured cards
 * @return table items
 */
global define list Overview() ``{
    // TODO FIXME: your code here...
    return [];
}

/**
 * delete line in netd_conf
 * @param line_number "iid" geted from table's item ID
 * @return map If success returns updated line
 */
global define deleteLine(any line_number) ``{
    // delete
    map current_line = select(filter(`line, netd_conf,
	``(lookup(line, "iid", "0") == line_number)), 0, $[]);
    // set "deleted" flag to true
    current_line = add(current_line, "changed", true);
    current_line = add(current_line, "deleted", true);
    netd_conf    = maplist(`line, netd_conf, ``{
	if (lookup(line, "iid", "0") == line_number) {
	    return(current_line);
	}
	else {
	    return(line);
	}
    });
};


/**
 * add a line in DB
 * @param new_line new_line contains new entry for global netd_conf configuration
 * @return void
 */
global define void addLine(map new_line) ``{
    // add
    new_line  = add(new_line, "changed", true);
    netd_conf = add(netd_conf, new_line);
    return;
}

/**
 * Change a line in DB
 * @param new_line new_line contains changes for entry in netd_conf
 * @param line_number line_number contains iid of changed entry in netd_conf
 * @return map If success, returnes updated line
 */
global define changeLine(map new_line, any line_number) ``{
    // entry was changed - so set "changed" flag to true
    new_line  = add(new_line, "changed", true);
    netd_conf = maplist(`line, netd_conf, ``{
	if (lookup(line, "iid", "0") == line_number) {
	    return(new_line);
	}
	else {
	    return(line);
	}
    });
};


/* EOF */
}
