/****************************************************************************

Copyright (c) 2000 - 2012 Novell, Inc.
All Rights Reserved.

This program is free software; you can redistribute it and/or
modify it under the terms of version 2 of the GNU General Public License as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.   See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, contact Novell, Inc.

To contact Novell about this file by physical or electronic mail,
you may find current contact information at www.novell.com

****************************************************************************/
/**
 * File:	include/inetd/wizards.ycp
 * Package:	Configuration of inetd
 * Summary:	Wizards definitions
 * Authors:	Petr Hadraba <phadraba@suse.cz>
 *		Martin Lazar <mlazar@suse.cz>
 *
 * $Id$
 */

{

textdomain "inetd";

import "Wizard";
import "Label";
import "Sequencer";
import "Confirm";
import "Package";
import "PackageSystem";

include "inetd/complex.ycp";
include "inetd/dialogs.ycp";

/**
 * Whole configuration of inetd
 * @return sequence result
 */
define symbol InetdSequence() ``{

    // agents barf if not root, #35363
    if (!Confirm::MustBeRoot ())
    {
	return `abort;
    }

    map aliases = $[
	"read"		: [ ``( ReadDialog() ), true ],
	"main"		:   ``( InetdDialog() ),
	"write"		: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();
    Wizard::SetDesktopTitleAndIcon("inetd");

    symbol ret = `abort;
    
    if (PackageSystem::CheckAndInstallPackagesInteractive(["xinetd"]))
        ret = Sequencer::Run (aliases, sequence);

    UI::CloseDialog();
    Wizard::RestoreScreenShotName();
    
    return ret;
}

/**
 * Whole configuration of inetd but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
define symbol InetdAutoSequence() ``{
    string caption = _("Xinetd Configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetDesktopTitleAndIcon("inetd");
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    Package::DoInstall(["xinetd"]);

    symbol ret = InetdDialog ();

    y2milestone("%1", ret);
    UI::CloseDialog();
    return ret;
}

/* EOF */
}
